plugins {
	id 'java'
	id 'groovy'
	id 'jacoco'
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'idea'
}

group = 'com.branch'
version = '0.0.1-SNAPSHOT'
description = 'Take home project for branch'

ext {
	set('springCloudVersion', "2025.1.1")
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

sourceSets {
	test {
		groovy {
			srcDirs += ['src/test/java']
		}
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}


repositories {
	mavenCentral()
}

dependencies {
	implementation 'com.hazelcast:hazelcast'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'org.springframework.boot:spring-boot-starter-hazelcast'
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	compileOnly 'org.projectlombok:lombok'
	implementation 'org.mapstruct:mapstruct:1.6.3'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
	testImplementation 'org.springframework.boot:spring-boot-starter-hazelcast-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.apache.groovy:groovy:4.0.26'
	testImplementation platform('org.spockframework:spock-bom:2.4-M4-groovy-4.0')
	testImplementation 'org.spockframework:spock-core'
	testImplementation 'org.spockframework:spock-spring'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy tasks.named('jacocoTestReport')
}

tasks.named('jacocoTestReport') {
	dependsOn tasks.named('test')
	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}
